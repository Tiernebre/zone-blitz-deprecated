plugins {
    id 'application'
    id "org.jooq.jooq-codegen-gradle" version "3.19.5"
}

application {
    mainClassName = 'com.tiernebre.App'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes["Main-Class"] = 'com.tiernebre.App'
    }
}

dependencies {
    implementation("io.javalin:javalin:6.1.2")
    implementation("io.jstach:jstachio:1.3.4")
    implementation("org.slf4j:slf4j-simple:2.0.12")
    implementation("org.apache.commons:commons-math3:3.6.1")
    implementation("org.apache.commons:commons-lang3:3.14.0")
    implementation("org.jooq:jooq:3.19.5")
    implementation("org.postgresql:postgresql:42.7.2")

    jooqCodegen("org.jooq:jooq-postgres-extensions:3.19.5")

    testImplementation("junit:junit:4.13.2")
    testImplementation("org.mockito:mockito-core:5.10.0")
    
    annotationProcessor("io.jstach:jstachio-apt:1.3.4")
}

jooq {
    configuration {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = System.getenv('ZONE_BLITZ_POSTGRES_JDBC_URL')
            user = System.getenv('ZONE_BLITZ_POSTGRES_USER')
            password = System.getenv('ZONE_BLITZ_POSTGRES_PASSWORD')
        }
        generator {
            name = 'org.jooq.codegen.JavaGenerator'
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
                excludes = 'public.schema_migrations'
            }
            target {
                packageName = 'com.tiernebre.database.jooq'
                directory = 'src/main/java'
            }
        }
    }
}
